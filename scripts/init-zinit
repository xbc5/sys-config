#!/bin/zsh

# WARN: this is sourced by /etc/zshrc; don't rely on user arguments, this MUST
# execute as-is.

if (( `id -u` >= 1000 )); then
  if [[ -e "/run/qubes/this-is-appvm" ]]; then
    should_reload=false
    ZINIT_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/zinit.git"

    if ! [[ -e "${ZINIT_HOME}" ]]; then
      print -P "%F{33}Installing %F{220}Zinit%F{33}...%f"
      mkdir -p "$(dirname $ZINIT_HOME)" && \
      git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"

      if (( $? != 0 )); then
	print -P "%F{120}Clone failed!%f"
	exit 1
      fi

      should_reload=true
    fi

    # must be before comp init
    source "${ZINIT_HOME}/zinit.zsh"

    # PLUG-INS
    # Zinit requires these, and automatically puts them into your config on installation.
    # Load a few important annexes, without Turbo (this is currently required for annexes)
    zinit light-mode for \
      zdharma-continuum/zinit-annex-as-monitor \
      zdharma-continuum/zinit-annex-bin-gem-node \
      zdharma-continuum/zinit-annex-patch-dl \
      zdharma-continuum/zinit-annex-rust
    # git
    zinit ice lucid wait
    zinit snippet OMZ::lib/git.zsh # provides git_current_branch(), and others
    zinit ice lucid wait
    zinit snippet OMZ::plugins/git/git.plugin.zsh
    # gitignore: gi
    zinit ice lucid wait
    zinit snippet OMZ::plugins/gitignore/gitignore.plugin.zsh
    # ssh-agent
    zinit ice silent wait
    zinit snippet OMZ::plugins/ssh-agent/ssh-agent.plugin.zsh
    # tmux
    zinit ice lucid wait
    zinit snippet OMZ::plugins/tmux/tmux.plugin.zsh
    # fzf command history
    zinit ice lucid wait'0'
    zinit light joshskidmore/zsh-fzf-history-search
    # fzf tab complete everything
    zinit light Aloxaf/fzf-tab
    zstyle ':fzf-tab:complete:cd:*' fzf-preview 'exa \
           --color=always --classify --icons --long --no-permissions \
           --no-time --git --no-filesize $realpath'
    # wd plug-in
    zinit ice wait lucid svn
    zinit snippet OMZ::plugins/wd
    # add git remotes: e.g. add-upstream | add-remote
    zinit light caarlos0-graveyard/git-add-remote

    # load auto-completion engine, must be after plug-ins, and before any cdreplay
    # https://github.com/zdharma/zinit#completions-2
    autoload -Uz compinit && compinit

    # docker
    zinit ice wait lucid atload"zpcdreplay" atpull"zinit creinstall -q ."
    zinit light greymd/docker-zsh-completion

    zinit cdreplay -q

    if [[ $should_reload == true ]]; then
      source "${HOME}/.zshrc" && \
        zinit self-update
    fi
  fi
fi
